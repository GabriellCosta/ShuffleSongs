apply plugin: 'jacoco'

task jacocoTestReport(type: JacocoReport) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            '**/di/**',
            '**/*ModuleKt**'
    ]

    def kotlinTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)

    def coverageSourceDirs = [
            "src/main/kotlin",
            "src/main/java"
    ]

    getClassDirectories().setFrom(files([kotlinTree]))

    getAdditionalSourceDirs().setFrom(files(coverageSourceDirs))
    getSourceDirectories().setFrom(files(coverageSourceDirs))

    getExecutionData().setFrom(fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code_coverage/debugAndroidTest/connected/*coverage.ec"
    ]))

    doLast {
        println "Jacoco report has been generated to file://${reports.html.destination}/index.html"
    }
}
